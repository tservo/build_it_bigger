apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.0'
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'version'
    productFlavors {
        free {
            dimension 'version'
            applicationIdSuffix '.free'
            versionNameSuffix '-free'
        }
        paid {
            dimension 'version'
            applicationIdSuffix '.paid'
            versionNameSuffix '-paid'
        }
    }

    // we were having trouble with META-INF being included more than once
    // suggested by StackOverflow.https://stackoverflow.com/questions/44342455/more-than-one-file-was-found-with-os-independent-path-meta-inf-license

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'com.android.support:appcompat-v7:' +
            rootProject.ext.supportLibVersion

    // ads service.  remove on paid flavor!
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'

    // google endpoints services
    implementation 'com.google.api-client:google-api-client:' +
            rootProject.ext.googleClientVersion
    implementation 'com.google.http-client:google-http-client-android:' +
            rootProject.ext.googleClientVersion

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // espresso tests
    androidTestImplementation('com.android.support.test.espresso:espresso-core:' +
            rootProject.ext.espressoCoreVersion, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation'com.android.support.test:runner:' +
            rootProject.ext.testRunnerVersion
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // idling resource has to be used in the regular build.
    implementation 'com.android.support.test.espresso:espresso-idling-resource:' +
            rootProject.ext.espressoCoreVersion

    // junit tests
    testImplementation 'junit:junit:' +
            rootProject.ext.junitLibVersion

    implementation project(path: ':jokesmith')
    implementation project(path: ':jokelibrary')
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
    }
}